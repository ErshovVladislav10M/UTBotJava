apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

configurations {
    lifetimedProcessMockCompileClasspath.extendsFrom configurations.compileClasspath
    processWithRdServerMockCompileClasspath.extendsFrom configurations.compileClasspath
}

sourceSets {
    lifetimedProcessMock {
        kotlin {
            srcDirs = ["src/main/lifetimedProcessMock"]
        }
    }
    processWithRdServerMock {
        kotlin {
            srcDirs = ["src/main/processWithRdServerMock"]
        }
    }
}

dependencies {
    implementation group: 'com.jetbrains.rd', name: 'rd-framework', version: '2022.2.5'
    implementation group: 'com.jetbrains.rd', name: 'rd-gen', version: '2022.2.5'
    implementation group: 'com.jetbrains.rd', name: 'rd-core', version: '2022.2.5'

    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlin_logging_version

    processWithRdServerMockImplementation project(':utbot-rd')
}

task lifetimedProcessMockJar (type: Jar) {
    dependsOn compileLifetimedProcessMockKotlin
    archiveAppendix.set("lifetimedProcessMock")

    manifest {
        attributes(
                'Main-Class': 'org.utbot.rd.tests.LifetimedProcessMockKt'
        )
    }

    from configurations.lifetimedProcessMockCompileClasspath.collect {
        it.isDirectory() ? it : zipTree(it)
    } + sourceSets.lifetimedProcessMock.output
}

task processWithRdServerMockJar (type: Jar) {
    dependsOn compileProcessWithRdServerMockKotlin
    archiveAppendix.set("processWithRdServerMock")

    manifest {
        attributes(
                'Main-Class': 'org.utbot.rd.tests.ProcessWithRdServerMockKt'
        )
    }

    from configurations.processWithRdServerMockCompileClasspath.collect {
        it.isDirectory() ? it : zipTree(it)
    } + sourceSets.processWithRdServerMock.output
}

test {
    dependsOn lifetimedProcessMockJar
    dependsOn processWithRdServerMockJar
    systemProperty("RD_MOCK_PROCESS", lifetimedProcessMockJar.archiveFile.get().getAsFile().canonicalPath)
    systemProperty("PROCESS_WITH_RD_SERVER_MOCK", processWithRdServerMockJar.archiveFile.get().getAsFile().canonicalPath)
}