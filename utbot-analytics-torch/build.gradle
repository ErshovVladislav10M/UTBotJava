apply from: "${parent.projectDir}/gradle/include/jvm-project.gradle"

configurations {
    torchmodels
}

def osName = System.getProperty('os.name').toLowerCase().split()[0]
if (osName == "mac") osName = "macosx"
String classifier = osName + "-x86_64"

evaluationDependsOn(':utbot-framework')
compileTestJava.dependsOn tasks.getByPath(':utbot-framework:testClasses')

dependencies {
    implementation(project(":utbot-framework"))
    api project(':utbot-analytics')
    compile(project(':utbot-instrumentation'))
    testImplementation group: 'junit', name: 'junit', version: junit4_version

    implementation group: 'org.bytedeco', name: 'javacpp', version: javacpp_version, classifier: "$classifier"

    implementation group: 'org.jsoup', name: 'jsoup', version: jsoup_version
    implementation "ai.djl:api:$djl_api_version"
    implementation "ai.djl.pytorch:pytorch-engine:$djl_api_version"
    implementation "ai.djl.pytorch:pytorch-native-auto:$pytorch_native_version"
}

test {
    useJUnitPlatform()
}

processResources {
    configurations.torchmodels.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from(zipTree(artifact.getFile())) {
            into "models"
        }
    }
}

jar {
    dependsOn classes
    manifest {
        attributes 'Main-Class': 'org.utbot.QualityAnalysisKt'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}